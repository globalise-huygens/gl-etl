SELECT *  {
?loc a <http://example.globalise.nl/temp/location>;
   	<http://example.globalise.nl/temp/location/_label> ?_label.
BIND (URI(replace(STR(?loc),"http://example.globalise.nl/temp/location/(.*)","https://data.globalise.huygens.knaw.nl/hdl:20.500.14722/place:$1")) AS ?location)

    
OPTIONAL { ?loc <http://example.globalise.nl/temp/location/glob_id> ?glob_id. }

OPTIONAL { ?loc <http://example.globalise.nl/temp/location/coord_certainty> ?coord_certainty. }

OPTIONAL { ?loc <http://example.globalise.nl/temp/location/comments> ?comments. }

OPTIONAL {        
    ?loc <http://example.globalise.nl/temp/location/latitude> ?latitude;
    	<http://example.globalise.nl/temp/location/longitude> ?longitude.
}

OPTIONAL { ?loc <http://example.globalise.nl/temp/location/point> ?point. }
    
OPTIONAL {
    ?_cs <http://example.globalise.nl/temp/location2coordsource/location> ?loc;
    	<http://example.globalise.nl/temp/location2coordsource/source> ?_coordsource.
	?_coordsource <http://example.globalise.nl/temp/source/page_no> ?coordsource_page_no;
    	<http://example.globalise.nl/temp/source/zotero_url> ?coordsource_zotero_url.
         OPTIONAL { ?_coordsource <http://example.globalise.nl/temp/source/comments> ?coordsource_comments. }
}

OPTIONAL {
	?_ls <http://example.globalise.nl/temp/location2source/location> ?loc;
    	<http://example.globalise.nl/temp/location2source/source> ?_source.
	?_source <http://example.globalise.nl/temp/source/page_no> ?source_page_no;
    	<http://example.globalise.nl/temp/source/zotero_url> ?source_zotero_url.
 OPTIONAL { ?_source <http://example.globalise.nl/temp/source/comments> ?source_comments. }
}

OPTIONAL {
	?_ll <http://example.globalise.nl/temp/locationlabel/location> ?loc;
        <http://example.globalise.nl/temp/locationlabel/label> ?_l.
        #er kan ook een timespan zijn
	?_l	<http://example.globalise.nl/temp/label/label> ?label;
    	<http://example.globalise.nl/temp/label/type> ?label_type.
  OPTIONAL { ?_l <http://example.globalise.nl/temp/label/comments> ?label_comments. }
    	#een label kan nog een source hebben
}

    OPTIONAL {
        ?_lcc <http://example.globalise.nl/temp/location2countrycode/location> ?loc;
        	 <http://example.globalise.nl/temp/location2countrycode/countrycode> ?_cc.
        ?_cc <http://example.globalise.nl/temp/countrycode/countrycode> ?countrycode;
        	 <http://example.globalise.nl/temp/countrycode/country> ?country.
    }
    
    OPTIONAL {
        ?lpof <http://example.globalise.nl/temp/locationpartof/child_location> ?loc;
        	<http://example.globalise.nl/temp/locationpartof/parent_location> ?rel;
        	<http://example.globalise.nl/temp/locationpartof/locationpartof> ?related_type.
        	BIND (URI(replace(STR(?rel),"http://example.globalise.nl/temp/location/(.*)","https://data.globalise.huygens.knaw.nl/hdl:20.500.14722/place:$1")) AS ?related)
    }
    
#FILTER(?loc=<http://example.globalise.nl/temp/location/fff802bd-bb40-466b-9c72-10d8c437912f>)
#FILTER(?loc=<http://example.globalise.nl/temp/location/d84a012d-9feb-424c-ab09-e93084e2be3a>)
}
#limit 100   